input trend_length = 20;
input show_trend = no;
input stoploss = .05; # 5% stoploss
input profittarget = .05; # 5% profit target
input gap_size = .01; # 1 percent

def SMA = Average(close, trend_length);
def BearishTrend = open[2] < SMA[2];

def GapDown = high[1] < low[2];  # current candle high is < previous candle low
def gapsize = if GapDown then (low[2] - high[1]) else 0;
def didwegapdown = if BearishTrend and GapDown and (gapsize / low[2]) > gap_size then 1 else 0;

def GapDownHighestPrice;
def GapDownFillPrice;
def bull_islandgap;
def uptarget;

if didwegapdown {
    GapDownHighestPrice = high[1];
    GapDownFillPrice = low[2];
    bull_islandgap = double.nan;
    uptarget = gapsize*2;
    # Check if we are still an island. low[1] > GapUpLowestPrice
    # did we have a gap down and did the high of gap down create island
} else if GapDownHighestPrice[1] and high[1] < GapDownFillPrice[1] and low[1] <= GapDownHighestPrice[1]  {
    # Check if the candle makes a new island high. 
    GapDownHighestPrice = if high[1] > GapDownHighestPrice[1] then high[1] else GapDownHighestPrice[1];
    GapDownFillPrice = GapDownFillPrice[1];
    bull_islandgap = double.nan;
    uptarget = uptarget[1];
    # Check if we have a gapup. And checks if there is an island gap.
} else if GapDownHighestPrice[1] and low[1] > high[2] and low[1] > GapDownHighestPrice[1] {
    GapDownHighestPrice = double.nan;
    GapDownFillPrice = double.nan;
    bull_islandgap = 1;
    #uptarget = close + uptarget[1];
    uptarget = close[1] + (close[1]*profittarget);
}else {
    GapDownHighestPrice = double.nan;
    GapDownFillPrice = double.nan;
    bull_islandgap = double.nan;
    uptarget = double.nan;
};

plot bull_islandgap_true = if bull_islandgap then 1 else 0;
